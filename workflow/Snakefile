import gzip
from snakemake.utils import min_version
min_version("7.25.0")

# Define wrapper versions
wrapper_ver = "v3.3.2"

include: "rules/utils.smk"
include: "rules/setup.smk"
include: "rules/raw.smk"
include: "rules/trim.smk"



#####################
### Read Trimming ###
#####################
rule multiqc:
    input:
        ### FastQC raw
        expand_pandas(get_rule_stats("fastqc_raw"), units[~is_units_align(units)]),
        ### Trimming
        expand_pandas(get_rule_stats("{trimmer}_fastq_pe".format(trimmer=config["reads"]["trim"]["tool"])), units[~is_units_align(units) & units.type.eq("pe")]) if is_activated("reads/trim") else [],
        expand_pandas(get_rule_stats("{trimmer}_fastq_se".format(trimmer=config["reads"]["trim"]["tool"])), units[~is_units_align(units) & units.type.eq("se")]) if is_activated("reads/trim") else [],
        ### FastqQC trimmed
        expand_pandas(get_rule_stats("fastqc_trim"), units[~is_units_align(units)]) if is_activated("reads/trim") else [],
    output:
        html = "reports/multiqc.html",
        data = "reports/multiqc_data.zip",
    log:
        "logs/reports/multiqc.log"
    benchmark:
        "benchmarks/reports/multiqc.log"
    localrule: True
    threads: 1
    resources:
        mem = lambda w, attempt: f"{10 * attempt} GB",
        runtime = lambda w, attempt: f"{30 * attempt} m",
    wrapper:
        wrapper_ver + "/bio/multiqc"

rule all:
    input:
        qc = rules.multiqc.output.html,
        res = expand_pandas(get_fastqs_trimmed(), units),
    message:
        "Read trimming finished successfully!"
    default_target: True
